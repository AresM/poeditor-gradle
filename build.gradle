apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'maven-publish'

// Artifact settings
group = 'be.lukin.poeditor'
version='0.2.0'
archivesBaseName = 'gradle'

def _name = 'Gradle Plugin for POEditor'
def _description = 'Gradle plugin to sync translations with POEditor'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'be.lukin.poeditor:poeditor-client:0.1.1'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'POEDitor Gradle Plugin', 'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId group
            artifactId archivesBaseName
            version version

            from components.java
        }
    }
}

def _ossrhUsername = this.properties['ossrhUsername']
def _ossrhPassword = this.properties['ossrhPassword']

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            
            //use: gradle uploadArchives -Premote
            if(this.hasProperty('remote')){
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: _ossrhUsername, password: _ossrhPassword)
                }
            } else {
                repository(url: uri('./build/repo'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: _ossrhUsername, password: _ossrhPassword)
            }

            pom.project {
                name _name
                packaging 'jar'
                artifactId archivesBaseName
                description _description
                url 'https://github.com/lukin0110/poeditor-gradle/'
                inceptionYear '2015'

                scm {
                    url 'https://github.com/lukin0110/poeditor-gradle/'
                    connection 'scm:https://github.com/lukin0110/poeditor-gradle.git'
                    developerConnection 'scm:git://github.com/lukin0110/poeditor-gradle.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'lukin0110'
                        name 'Maarten Huijsmans'
                        email 'maarten@lukin.be'
                    }
                }
                
                issueManagement {
                    system 'GitHub issues'
                    url 'https://github.com/lukin0110/poeditor-gradle/issues'
                }
            }
        }
    }
}
